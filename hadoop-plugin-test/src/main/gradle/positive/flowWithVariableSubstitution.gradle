buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath files("${project.pluginTestDir}/hadoop-plugin-${project.version}.jar", "${project.pluginTestDir}/hadoop-plugin-${project.version}-SNAPSHOT.jar")
    // Must manually specify snakeyaml for testing, not required for users
    classpath 'org.yaml:snakeyaml:1.18'
  }
}

apply plugin: com.linkedin.gradle.hadoop.HadoopPlugin

// Test generating a flow YAML file with variable substitution "${}" in the job properties, job name, dependencies and conditions

def projectPath = "."
def jobName = "uploadResourceFilesJob"

hadoop {
  buildPath "jobs/flowWithVariableSubstitution"
  cleanPath true
  generateYamlOutput true

  propertyFile('common') {
    set properties: [
      'user.to.proxy': 'foo'
    ]
  }

  workflow('wordCountFlow') {
    hadoopShellJob("${jobName}") {
      usesCommands([
          "hadoop fs -mkdir -p ${projectPath}",
          "hadoop fs -copyFromLocal -f ./text ${projectPath}"
      ])
    }

    hadoopJavaJob('wordCountMapReduceJob') {
      uses 'com.linkedin.hadoop.example.WordCountJob'
      jvmClasspath './lib/*'
      reads files: [
          'input.path': "${projectPath}/text"
      ]
      writes files: [
          'output.path': "${projectPath}/word-count-map-reduce"
      ]
      set confProperties: [
          'mapreduce.job.classloader': true
      ]
      set properties: [
          'force.output.overwrite': true
      ]
      depends "${jobName}"
      conditions '${' + "${jobName}" + ':param1} == "foo"'
    }

    def keywordToCheck = "Government"

    hadoopShellJob('checkResultsJob') {
      uses "bash ./bash/checkResults.sh ${projectPath} ${keywordToCheck}"
      depends 'wordCountMapReduceJob'
    }

    targets 'checkResultsJob'
  }
}
